pkgname=tes3mp-raknet-git
pkgver=r108.9ace90a
pkgrel=1
pkgdesc="Cross platform C++ network library"
arch=('i686' 'x86_64')
url="https://github.com/TES3MP/RakNet.git"
license=('custom')
depends=('gcc-libs')
makedepends=('cmake')
provides=('raknet')
conflicts=('raknet' 'raknet-git' 'raknet-3.261')
source=("git+https://github.com/tes3mp/RakNet.git")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/RakNet"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}



build()
{
  cd ${srcdir}/RakNet
  mkdir -p build && pushd build
  cmake -DCMAKE_BUILD_TYPE=Release \
    -DRAKNET_ENABLE_DLL=ON \
    -DRAKNET_ENABLE_SAMPLES=OFF \
    -DRAKNET_ENABLE_STATIC=ON \
    ..
  make 
}

package()
{
  # DESTDIR is not used correctly so copy library and its headers manually
  install -d "${pkgdir}"/usr/{lib,include}/raknet
  install -D -m644 ${srcdir}/RakNet/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -Dm644 "${srcdir}"/RakNet/include/raknet/*.h "${pkgdir}"/usr/include/raknet
  install -m644 "${srcdir}"/RakNet/build/lib/libRakNetDLL.so "${pkgdir}"/usr/lib/raknet
  install -m644 "${srcdir}"/RakNet/build/lib/libRakNetLibStatic.a "${pkgdir}"/usr/lib/raknet
}
